         TITLE 'IEDAYD - TSO DESTINATION SCHEDULER'                     01000020
IEDAYD   CSECT                                                          02000020
*A450000                                                         S22029 02001022
*C420000,430000                                                  S22029 02002022
*D493000-496000                                                  S22029 02003022
*A490000,510000,530000                                           A42371 02005020
*C022000,022100,026300,410000                                    Y06327 02006005
*A022050,025620,275000,285000,331000-335000                      Y06327 02007005
*A551000-556000,700700-709100,845000                             Y06327 02008005
*A333000,472000,551000,706300-710000                           @Y17XAYP 02008200
*C472000-476000,705600                                         @Y17XAYP 02008400
*********************************************************************** 02010020
*MODULE NAME = IEDAYD  (TCAM,TSO)                              @Y17XAYP 02020000
*                                                              @Y17XAYP 02020400
*DESCRIPTIVE NAME = TIME SHARING DESTINATION SCHEDULER         @Y17XAYP 02020800
*                                                              @Y17XAYP 02021200
*COPYRIGHT = NONE                                              @Y17XAYP 02021600
*                                                                     * 02030020
*STATUS -- VERSION 10.0                                        @Y17XAYP 02040000
*                                                                     * 02050020
*FUNCTION -- THE TIME SHARING DESTINATION SCHEDULER GETS CONTROL IN   * 02060020
*   PLACE OF THE TCAM DESTINATION SCHEDULER WHEN TSO IS IN THE SYSTEM.* 02070020
*   IF THIS ROUTINE WAS ACTIVATED BECAUSE A TCAM BUFFER WAS POSTED TO * 02080020
*   IT, IT LOADS THE ADDRESS OF THE TCAM DESTINATION SCHEDULER'S STCB * 02090020
*   (IEDQHM) INTO REGISTER 3, AND BRANCHES TO THE DISPATCHER BYPASS   * 02100020
*   FUNCTION TO IMMEDIATELY ACTIVATE THE STCB.                        * 02110020
*      IF THIS ROUTINE WAS ACTIVATED BY THE POSTING OF A TIME SHARING * 02120020
*   DESTINATION QCB TO ITSELF, IT SETS THE POSTED BIT OFF IN THE QCB  * 02130020
*   AND CHECKS TO SEE IF A SIMULATED ATTENTION READ WAS REQUESTED.    * 02140020
*   IF IT WAS, CONTROL IMMEDIATELY PASSES TO THE DISPATCHER DISPATCH  * 02150020
*   FUNCTION.  IF NOT, THIS ROUTINE CHECKS TO SEE IF TPUT REQUESTED   * 02160020
*   A WRITE BREAK.  IF NOT, CONTROL ALSO PASSES TO THE DISPATCH       * 02170020
*   FUNCTION.  IF A WRITE BREAK WAS REQUESTED, THIS ROUTINE LOADS THE * 02180020
*   LCB ADDRESS INTO REGISTER 4 AND BRANCHES TO THE TIME SHARING      * 02190020
*   SCHEDULER (IEDAYZ IF ATTACHED VIA 270X, IEDAYJ IF ATTACHED        * 02200005
*   VIA 3705) TO DETERMINE WHETHER A WRITE BREAK CHANNEL COMMAND CAN  * 02205005
*   BE ISSUED.  IF IT CAN, IEDAYZ OR IEDAYJ ISSUES AN IOHALT.  IN     * 02210005
*   EITHER CASE, CONTROL RETURNS TO THIS ROUTINE, WHICH THEN PASSES   * 02220020
*   CONTROL TO THE DISPATCHER DISPATCH FUNCTION.                      * 02230020
*                                                                     * 02240020
*ENTRY POINT -- IEDAYD - TO INITIATE A WRITE BREAK OPERATION, IF      * 02250020
*   NECESSARY, AND TO ASSIGN A BUFFER OR QCB TO ITS DESTINATION.      * 02260020
*   CALLING SEQUENCE          L    R15,STCBLINK-1                     * 02270020
*                             BR   R15                                * 02280020
*                                                                     * 02290020
*INPUT -- IEDAYD IS CALLED BY THE DISPATCHER WHEN A TCAM BUFFER IS    * 02300020
*   POSTED TO IT, OR WHEN A TIME SHARING DESTINATION QCB IS POSTED    * 02310020
*   TO ITSELF.  AT ENTRY THE FOLLOWING REGISTERS ARE SET.             * 02320020
*   R1 HAS THE ADDRESS OF THE TCAM BUFFER OR TIME SHARING QCB.        * 02330020
*   R3 HAS THE STCB ADDRESS.                                          * 02340020
*   R7 HAS THE DESTINATION QCB ADDRESS.                               * 02350020
*   R11 HAS THE DISPATCHER ADDRESS.                                   * 02360020
*   R13 HAS THE ADDRESS OF AVTSAVE2.                                  * 02370020
*   R15 HAS THE ENTRY POINT ADDRESS.                                  * 02380020
*                                                                     * 02390020
*OUTPUT -- ON EXIT TO THE DISPATCHER BYPASS FUNCTION, REGISTER 3 CON- * 02400020
*   TAINS THE ADDRESS OF THE TCAM DESTINATION SCHEDULER'S STCB.       * 02410020
*      ON EXIT TO THE (NON-NCP) TIME SHARING SCHEDULER, THE FOLLOWING * 02420000
*   REGISTERS ARE SET.                                                * 02430000
*   R2 HAS THE RETURN ADDRESS IN IEDAYD.                              * 02440020
*   R3 HAS THE RETURN ADDRESS IN IEDAYD.                              * 02450020
*   R4 HAS THE LCB ADDRESS.                                           * 02460020
*   R7 HAS THE DESTINATION QCB ADDRESS.                               * 02470020
*   R10 HAS THE DCB ADDRESS.                                          * 02480020
*   R11 HAS THE DISPATCHER ADDRESS.                                   * 02490020
*   R13 HAS THE ADDRESS OF AVTSAVE2.                                  * 02500020
*      ON EXIT TO THE DISPATCHER DISPATCH FUNCTION, THE POSTED BIT IN * 02510020
*   THE TIME SHARING QCB IS OFF.                                      * 02520020
*                                                                     * 02530020
*          ON EXIT TO THE (NCP) TSO SCHEDULER THE FOLLOWING           * 02530100
*   REGISTERS ARE SET.                                                * 02530200
*   R0 HAS TSO SCHEDULER ROUTINE SELECT VALUE                         * 02530300
*   R7 HAS A DESTINATION QCB ADDRESS                                  * 02530400
*   R10 HAS A LGB ADDRESS                                             * 02530500
*   R11 HAS THE ADDRESS OF THE TCAM DISPATCHER                        * 02530600
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   * 02530700
*   R14 HAS THE RETURN ADDRESS                                        * 02530800
*   R15 HAS THE TSO SCHEDULER ENTRY ADDRESS                           * 02530900
*EXTERNAL ROUTINE -- IEDAYZ - TO DETERMINE WHETHER THE WRITE BREAK    * 02540020
*   CHANNEL COMMAND REQUESTED BY TPUT CAN BE ISSUED, AND TO ISSUE AN  * 02550020
*   IOHALT ON THE LINE IF IT CAN.                                     * 02560020
*                    IEDAYJ - 3705 VERSION OF TSO DESTINATION         * 02562005
*   SCHEDULER, TO ISSUE AN IOHALT IF WRITE BREAK REQUESTED CAN BE     * 02564005
*   ISSUED.                                                           * 02566005
*                                                                     * 02570020
*EXITS-NORMAL -- TO THE DISPATCHER BYPASS FUNCTION.                   * 02580020
*   TO THE DISPATCHER DISPATCH FUNCTION.                              * 02590020
*                                                                     * 02600020
*EXITS-ERROR -- NONE                                                  * 02610020
*                                                                     * 02620020
*TABLES/WORKAREAS -- AVT, LCB, QCB, DCB, STCB, TSID, LGB.             * 02630005
*                                                                     * 02640020
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     * 02650020
*   PROBLEM PROGRAM MODE                                              * 02660020
*                                                                     * 02670020
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         * 02680020
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      * 02690020
*   SET.                                                              * 02700020
*                                                                     * 02710020
*********************************************************************** 02720020
         SPACE 2                                                        19000020
*        REGISTER EQUATES                                               20000020
         SPACE                                                          21000020
RSELECT  EQU   0                        TSO SCHED RTN SELECT   @Y17XAYP 21200000
RTRM     EQU   1                        ADDRESS OF TERM ENTRY, WORK REG 22000020
R2       EQU   2                        WORK REGISTER                   23000020
RSTCB    EQU   3                        ADDRESS OF STCB                 24000020
RLCB     EQU   4                        ADDRESS OF LCB                  25000020
R6       EQU   6                        ADDRESS OF TS SCHEDULER         26000020
RQCB     EQU   7                        ADDRESS OF DESTINATION QCB      27000020
RNQCB    EQU   8                        ADDRESS OF QCB PREFIX    Y06327 27500005
RDCB     EQU   10                       ADDRESS OF DCB                  28000020
RLGB     EQU   10                       ADDRESS OF LGB           Y06327 28500005
RDISP    EQU   11                       ADDRESS OF DISPATCHER           29000020
RBASE    EQU   12                       BASE REGISTER                   30000020
R13      EQU   13                       ADDRESS OF AVTSAVE2             31000020
RETURN   EQU   14                       RETURN ADDRESS REG     @Y17XAYP 31200000
R15      EQU   15                       HAS BASE ADDRESS AT ENTRY       32000020
         SPACE 2                                                        33000020
*        MASKS                                                          33100005
         SPACE                                                          33200005
LGBINDIC EQU   X'80'                    LGB INDICATOR            Y06327 33300005
         SPACE 2                                                        33500005
*        ADDRESSABILITY                                                 34000020
         SPACE                                                          35000020
         USING AVTSAVE2,R13                                             36000020
         USING IEDQDISP,RDISP                                           37000020
         USING IEDQLCB,RLCB                                             38000020
         USING IEDQQCB,RQCB                                             39000020
         USING IEDQSTCB,RSTCB                                           40000020
         USING IEDNLGB,RLGB                                      Y06327 41000005
         DC    AL1(DSPMCPL8),AL3(0),A(0)  STCB FIELDS            S22029 42000022
         USING *,RBASE                                                  44000020
         LR    RBASE,R15                ESTABLISH ADDRESSABILITY        45000020
IEDAYD  IEDHJN  AYD000                                                  45500022
         CLI   QCBSTVTO-IEDQQCB(RTRM),DSPSNDSC PASSED ELEMENT A QCB?    46000020
         BE    AYD100                   BRANCH ON YES                   47000020
         CLI   QCBSTVTO-IEDQQCB(RTRM),DSPSEND  IS THIS ELEMENT @Y17XAYP 47200000
*                                      A QCB FOR TERMINAL      @Y17XAYP 47220000
*                                      ATTACHED VIA NCP        @Y17XAYP 47240000
         BE    AYD100                  BRANCH YES                Y06327 47600005
         L     RSTCB,AVTTSOPT          GET THE ADDRESS OF        S22029 48000022
         L     RSTCB,TSIDEST-IEDQTSI(,RSTCB) TCAM DEST SCHED STCBS22029 48500022
         B     DSPBYPAS                 GIVE HM CONTROL                 49000020
AYD100   EQU   *                                                        50000020
         NI    QCBTSOF2,X'FF'-QCBPOSTO  TURN OFF QCB POSTED BIT         51000020
         TM    QCBTSOF1,QCBDISC         LOGGING OFF              A42371 51300020
         BO    AYD105                   BRANCH YES TO ALLOW BREAKA42371 51600020
         TM    QCBTSOF1,QCBSATRD        SIMULATED ATTENTION READ?       52000020
         BO    DSPDISP                  BRANCH ON YES TO RETURN         53000020
AYD105   EQU   *                                                 A42371 53500020
         TM    QCBTSOF1,QCBWRBRK        WRITE BREAK REQUESTED?          54000020
         BZ    DSPDISP                  BRANCH ON NO TO RETURN          55000020
         L     R6,AVTTSOPT              ADDR OF TIME SHARING QCB Y06327 55100005
         USING IEDQTSI,R6               TSINPUT QCB ADDRESS    @Y17XAYP 55120000
         L     RLGB,QCBLGBAD-1          GET ADDR OF LGB/DCB      Y06327 55200005
         TM    LGBDSORG,LGBINDIC        ATTACHED VIA A 3705      Y06327 55300005
         BO    AYD210                   YES, USE 3705 TSO SCHED  Y06327 55400005
         DROP  RLGB                                              Y06327 55500005
         USING IHADCB,RDCB                                       Y06327 55600005
         SR    RTRM,RTRM                                                56000020
         IC    RTRM,QCBRELLN            QCB'S STARTING RLN              57000020
         SR    R2,R2                                                    58000020
         IC    R2,DCBEIOBX              GET LCB SIZE                    60000020
         MR    RTRM-1,R2                GET STARTING LCB OFFSET         61000020
         AL    RTRM,DCBIOBAD            POINT TO IOB OF LCB             62000020
         SH    RTRM,LCBSIZE             ADJUST TO START OF LCB          63000020
         LR    RLCB,RTRM                LCB BASE                        64000020
         LA    R2,AYD200                SET UP WITH RETURN ADDRESS      65000020
         L     R6,TSISCHED-IEDQTSI(,R6) GET ADDRESS OF TS SCHEDULER     67000020
         BAL   RSTCB,28(,R6)            BRANCH TO ROUTINE               68000020
AYD200   EQU   *                                                        69000020
         B     DSPDISP                  RETURN                          70000020
AYD210   EQU   *                                                 Y06327 70070005
         LA    R2,QCBPRFSZ              GET SIZE OF QCB PREFIX   Y06327 70140005
         LR    RNQCB,RQCB               GET QCB BASE             Y06327 70210005
         SR    RNQCB,R2                 BACK UP TO PREFIX        Y06327 70280005
         USING IEDNQCB,RNQCB            ESTABLISH ADDRESSABILITY Y06327 70350005
*                                       FOR THE QCB PREFIX       Y06327 70420005
         L     RLCB,QCBPLCBA-1          GET ADDRESS OF PLCB      Y06327 70490005
         L     R15,TSIRNSCH             GET THE ADDRESS OF THE @Y17XAYP 70560000
*                                       TSO 3705 SCHEDULER       Y06327 70630005
         LA    RSELECT,TSOA01           TSO SCHEDULER ROUTINE  @Y17XAYP 70632000
         BALR RETURN,R15                GOT TO TSOSCHEDULER    @Y17XAYP 70634000
*                                       TO PROCESS WRITE BREAK @Y17XAYP 70636000
         B     DSPDISP                  EXIT TO DISPATCHER     @Y17XAYP 70638000
         SPACE 2                                                        71000020
*        CONSTANTS                                                      72000020
         SPACE                                                          73000020
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    SIZE OF LCB FROM RCB TO IOB     74000020
         EJECT                                                          75000020
         TLCBD                                                          76000020
         EJECT                                                          77000020
         TQCBD                                                          78000020
         TSOECDD                                                 Y06327 79000005
         TTSID                                                          80000020
         TAVTD                                                          81000020
         TDISPD                                                         82000020
         DCBD  DSORG=TX                                                 83000020
         TSTCBD                                                         84000020
         TLGBD                                                   Y06327 84500005
         END                                                            85000020
